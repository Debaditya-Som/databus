PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX databus: <https://databus.dbpedia.org/>

SELECT DISTINCT ?artifactUri ?dataset ?label ?desc ?license ?publisherUri ?groupUri ?date (?maxVersion AS ?version)  WHERE {
  GRAPH ?g {
    ?dataset dataid:artifact ?artifactUri.
    ?dataset dct:hasVersion ?maxVersion.
    ?dataset rdfs:label ?label.
    ?dataset rdfs:comment ?desc.
    ?dataset dct:issued ?date.
    ?dataset dct:publisher ?publisher.
    ?publisher foaf:account <%PUBLISHER_URI%> .
    ?dataset dataid:group ?groupUri.
    ?dataset dct:license ?license.
    {
      SELECT ?artifactUri (max(?version) as ?maxVersion) WHERE {
        ?artifactUri a dataid:Artifact .
        ?dataset dataid:artifact ?artifactUri.
        ?dataset dct:hasVersion ?version .
      }
      GROUP BY ?artifactUri
    }
    BIND(<%PUBLISHER_URI%> AS ?publisherUri)
  }
}
ORDER BY desc(?date)
