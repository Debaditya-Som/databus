PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX databus:  <https://databus.dbpedia.org/system/voc/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX s4ac: <http://ns.inria.fr/s4ac/v2#>

SELECT ?account ?label ?imageUrl ?created ?webIdURI ?about 
  (group_concat(?featured;separator="\n") as ?featuredContent) 
  (group_concat(?authorizedAccount;separator="\n") as ?authorizedAccounts) 
WHERE
{
  GRAPH ?g {
    ?account foaf:account <%ACCOUNT_URI%> .
    OPTIONAL { ?account foaf:name ?label . }
    OPTIONAL { ?account foaf:img ?imageUrl . }
    OPTIONAL { ?account dct:created ?created . } 
    OPTIONAL { ?account databus:webid ?webIdURI . }
    OPTIONAL { ?account rdfs:comment ?about . }
    OPTIONAL { ?account databus:featuredContent ?featured . }
    OPTIONAL {
      ?privilege a s4ac:AccessPolicy .
      ?privilege dct:creator  <%ACCOUNT_URI%> .
      ?privilege dct:subject ?authorizedAccount . 
    }
  }
}
GROUP BY ?account ?label ?imageUrl ?created ?webIdURI ?about
