PREFIX databus: <https://dataid.dbpedia.org/databus#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# latest version of all artifacts -> order by date -> and show infos for the 10 last
SELECT DISTINCT ?artifact as ?uri ?latestVersion ?latestVersionTitle
  ?latestVersionAbstract ?latestVersionDescription ?latestVersionDate WHERE {
 
  {
      SELECT ?artifact (max(STR(?version)) as ?latestVersion) WHERE {
        GRAPH ?g2 {
          ?dataset databus:artifact ?artifact.
          ?dataset dct:hasVersion ?version .
        }
      }
      GROUP BY ?artifact
  }
  GRAPH ?g1 {
    ?dataset databus:artifact ?artifact.
    ?dataset dct:hasVersion ?latestVersion .
    ?dataset dct:title ?latestVersionTitle .
    ?dataset dct:abstract ?latestVersionAbstract .
    ?dataset dct:description ?latestVersionDescription .
    ?dataset dct:issued ?latestVersionDate .
  }
}
ORDER BY desc(?latestVersionDate)
LIMIT 10
