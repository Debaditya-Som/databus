PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX databus: <https://databus.dbpedia.org/>

SELECT DISTINCT ?artifact AS ?uri ?title ?abstract ?description ?latestVersion ?latestVersionTitle
  ?latestVersionAbstract ?latestVersionDescription ?latestVersionDate ?group ?license ?attribution WHERE
{
 GRAPH ?g1 {
    ?dataset dataid:artifact ?artifact .
    OPTIONAL { ?artifact dct:title ?title . }
    OPTIONAL { ?artifact dct:abstract ?abstract . }
    OPTIONAL { ?artifact dct:description ?description . }
    ?dataset dct:hasVersion ?latestVersion .
    ?dataset dct:title ?latestVersionTitle .
    ?dataset dct:abstract ?latestVersionAbstract .
    ?dataset dct:description ?latestVersionDescription .
    ?dataset dct:issued ?latestVersionDate .
    ?dataset dataid:group ?group .
    ?dataset dct:license ?license .
    OPTIONAL { ?dataset dataid:attribution ?attribution . }
    {
      SELECT ?artifact (max(?version) as ?latestVersion) WHERE {
        GRAPH ?g2 {
          ?artifact a dataid:Artifact .
          ?dataset dataid:artifact ?artifact .
          ?dataset dct:hasVersion ?version .
          ?dataset dct:publisher ?publisher .
          GRAPH ?g3 {
            ?publisher foaf:account <%PUBLISHER_URI%> .
          }
        }
       
      }
      GROUP BY ?artifact
    }
  }
} ORDER BY desc(?latestVersionDate)