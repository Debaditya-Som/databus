PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX databus: <https://databus.dbpedia.org/>

SELECT DISTINCT ?artifactUri ?label ?abstract ?license ?groupUri ?groupLabel ?groupAbstract ?groupDescription ?date (?maxVersion AS ?version)  WHERE {
  GRAPH ?g1 {
    ?dataset dataid:artifact ?artifactUri .
    ?dataset dct:hasVersion ?maxVersion .
    ?dataset dct:title ?label .
    ?dataset dct:abstract ?abstract .
    ?dataset dct:issued ?date .
    ?dataset dataid:maintainer|dct:publisher ?publisher .
    ?dataset dataid:group ?groupUri .
    OPTIONAL { ?groupUri dct:title ?groupLabel . }
    OPTIONAL { ?groupUri dct:abstract ?groupAbstract . }
    OPTIONAL { ?groupUri dct:description ?groupDescription . }
    ?dataset dct:license ?license .
    {
      SELECT ?artifactUri (max(?version) as ?maxVersion) WHERE {
        GRAPH ?g2 {
          ?dataset dataid:artifact ?artifactUri .
          ?dataset dct:hasVersion ?version .
        }
      }
      GROUP BY ?artifactUri
    }
  }
  GRAPH ?g3 {
    ?publisher foaf:account <%ACCOUNT_URI%> .
  }
}
ORDER BY desc(?date)