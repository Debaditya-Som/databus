PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dataid-cv: <http://dataid.dbpedia.org/ns/cv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat:  <http://www.w3.org/ns/dcat#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?property ?value WHERE {

  GRAPH ?g {
    ?dataset dataid:version <%VERSION_URI%> .
    ?dataset dcat:distribution ?distribution .

    {
      ?distribution dct:hasVersion ?value .
      BIND(dct:hasVersion AS ?property)
    }
    UNION
    {
      ?distribution dataid:compression ?value .
      BIND(dataid:compression AS ?property)
    }
    UNION
    {
      ?distribution dataid:formatExtension ?value .
      BIND(dataid:formatExtension AS ?property)
    }
    UNION
    {
      ?distribution ?property ?value .
      ?property rdfs:subPropertyOf dataid:contentVariant .
    }
  }
}
