PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dataid: <http://dataid.dbpedia.org/ns/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# publisher with number and size of versions
SELECT DISTINCT ?accountUri ?numUploads ?uploadSize WHERE {

  ?publisher foaf:account ?accountUri.
  {
    SELECT ?publisher (count(?version) as ?numUploads) WHERE {
      ?dataset dct:publisher ?publisher.
      ?dataset dataid:version ?version .
    }
    GROUP BY ?publisher
  }
  {
    SELECT ?publisher (sum(?size) as ?uploadSize) WHERE {
      ?dataset dct:publisher ?publisher.
      ?dataset dcat:distribution ?distribution .
      ?distribution dcat:byteSize ?size .
    }
    GROUP BY ?publisher
  }
}
ORDER BY DESC(?uploadSize)
LIMIT 10
