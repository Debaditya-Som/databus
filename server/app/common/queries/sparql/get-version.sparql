
PREFIX databus: <https://dataid.dbpedia.org/databus#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

PREFIX dataiddebug: <http://dataid.dbpedia.org/ns/debug.ttl#>

# Get the information of the latest version for a specific artifact
SELECT DISTINCT ?version ?name ?title ?artifactTitle ?abstract ?description
  ?date ?group ?artifact ?publisher ?license ?attribution
  ?codeReference ?documentationLocation ?feedbackChannel ?issueTracker WHERE {

  GRAPH ?g {
    VALUES ?version { <%VERSION_URI%> }
    ?version dct:hasVersion ?name.
    ?version dct:title ?title.
    ?version dct:abstract ?abstract.
    ?version dct:description ?description.
    ?version dct:issued ?date.
    ?version databus:group ?group .
    ?version databus:artifact ?artifact.
    ?version dct:publisher ?publisher.
    ?version dct:license ?license.
    OPTIONAL { ?artifact dct:title ?artifactTitle . }
    OPTIONAL { ?version databus:attribution ?attribution . }
    OPTIONAL { ?version dataiddebug:codeReference ?codeReference . }
    OPTIONAL { ?version dataiddebug:documentationLocation ?documentationLocation . }
    OPTIONAL { ?version dataiddebug:feedbackChannel ?feedbackChannel . }
    OPTIONAL { ?version dataiddebug:issueTracker ?issueTracker . }
  }
}
